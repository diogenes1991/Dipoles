#-
#:MaxTermSize 5M
off statistics;
#include declarations.h
#include config.inc

#define diagfile1 "`leftDiagDefFile'"
#define diagfile2 "`rightDiagDefFile'"
#define suffix1 "l"
#define suffix2 "r"
.global

#do i = 1,2
    #include `diagfile`i'' # global
    #if `subsetNLODiagrams' == 1 && `i' == 1
        #define first "`subsetfirst'"
        #define last "`subsetlast'"
    #endif
    cf acc;
    cf dtag;
    .global

    #do j = `first', `last'
        load sav/diag`suffix`i''`j'.sav;
    #enddo

    #message summing up
    g expr`i' = 
    #do j = `first', `last'
        + dtag(`j')*diag`suffix`i''`j'
    #enddo
    ;
    .store;

    #message color
    l dd = expr`i';
    b color, delta, delta3;
    .sort
    collect acc;
    id acc(?args) = 1;
    b color;
    .sort
    Keep Brackets;
    id color(n1?) = n1;
    b cOlT, cOlf, cc, delta, delta3;
    .sort
    Keep Brackets;
    #write<`idDir'/color`suffix`i''.out> "color = %E;", dd;
    .store

    #message spinor chains

    l dd = expr`i';
    b SCC, VB, e_;
    .sort
    collect acc;
    id acc(?args) = 1;

    b SCC, VB, e_;
    .sort
    Keep Brackets;
    #write<`idDir'/SMEs`suffix`i''.out> "smes = %E;", dd;

    b SCC;
    .sort
    collect acc;
    id acc(?args) = 1;

    b SCC;
    .sort
    Keep Brackets;
    #write<`idDir'/SCCs`suffix`i''.out> "sccs = %E;", dd;
    .store

#enddo
.end
